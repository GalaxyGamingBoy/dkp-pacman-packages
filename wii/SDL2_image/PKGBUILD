# Maintainer:  Dave Murphy <davem@devkitpro.org>
# Contributor: Cpasjuste <cpasjuste@gmail.com>
# Contributor: carstene1ns <dev f4ke de>

pkgname=wii-sdl2_image
pkgver=2.8.2
pkgrel=1
pkgdesc="SDL2 image loading library."
arch=('any')
url="https://libsdl.org/projects/SDL_image/"
license=("zlib")
options=(!strip libtool staticlibs)
makedepends=('wii-pkg-config' 'dkp-toolchain-vars')
depends=(
  'wii-sdl2'
  'ppc-libpng'
  'ppc-libjpeg-turbo'
)
source=("${url}release/SDL2_image-${pkgver}.tar.gz")

groups=('wii-portlibs' 'wii-sdl2-libs')

build() {
  cd SDL2_image-$pkgver

  source ${DEVKITPRO}/wiivars.sh

  sed '/^noinst_PROGRAMS/d' -i Makefile.in

  ./configure --prefix="${PORTLIBS_PREFIX}" \
    --host=powerpc-eabi --disable-shared --enable-static \
    --disable-sdltest \
    --with-sdl-prefix=${PORTLIBS_PREFIX}

  make
}

package() {
  cd SDL2_image-$pkgver

  source /opt/devkitpro/wiivars.sh

  make DESTDIR="$pkgdir" install

  # add our static libs
  echo "Requires.private: libpng libjpeg" >> "${pkgdir}/${PORTLIBS_PREFIX}/lib/pkgconfig/SDL2_image.pc"

  # License
  #install -Dm644 "COPYING.txt" "${pkgdir}/${PORTLIBS_PREFIX}/licenses/${pkgname}/COPYING.txt"
}
sha256sums=('8f486bbfbcf8464dd58c9e5d93394ab0255ce68b51c5a966a918244820a76ddc')
