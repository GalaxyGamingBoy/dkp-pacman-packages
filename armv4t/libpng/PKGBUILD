# Maintainer:  Dave Murphy <davem@devkitpro.org>

pkgname=armv4t-libpng
pkgver=1.6.48
pkgrel=1
pkgdesc='PNG format graphic files library'
arch=('any')
url='http://www.libpng.org'
license=('custom')
options=(!strip libtool staticlibs)
depends=('armv4t-zlib')
groups=('armv4t-portlibs')
makedepends=('dkp-toolchain-vars')
source=("https://download.sourceforge.net/libpng/libpng-$pkgver.tar.xz")

build() {
  cd libpng-$pkgver

  source /opt/devkitpro/armv4tvars.sh

  ./configure --prefix="${PORTLIBS_PREFIX}" --host=arm-none-eabi \
    --disable-tools --disable-shared --enable-static
  make
}

package() {
  cd libpng-$pkgver

  source /opt/devkitpro/armv4tvars.sh

  make LN_S=cp DESTDIR="$pkgdir" install
  # license
  install -Dm644 LICENSE "$pkgdir"${PORTLIBS_PREFIX}/licenses/$pkgname/LICENSE
  # remove useless stuff
  rm -r "$pkgdir"${PORTLIBS_PREFIX}/share
}
sha256sums=('46fd06ff37db1db64c0dc288d78a3f5efd23ad9ac41561193f983e20937ece03')
