# Maintainer:  Dave Murphy <davem@devkitpro.org>

pkgname=3ds-libarchive
pkgver=3.7.4
pkgrel=1
pkgdesc='library that can create and read several streaming archive format (for Nintendo 3DS homebrew development)'
arch=('any')
url="https://www.libarchive.org"
license=('Simplified BSD')
options=(!strip libtool staticlibs)
source=(
 "http://www.libarchive.org/downloads/libarchive-${pkgver}.tar.gz"
 "libarchive-${pkgver}.patch"
)

makedepends=('3ds-pkg-config' 'dkp-toolchain-vars')
depends=('3ds-bzip2' '3ds-zlib' '3ds-liblzma' '3ds-libzstd')
groups=('3ds-portlibs')

build() {
  cd libarchive-$pkgver

  source /opt/devkitpro/3dsvars.sh

  patch -Np1 -i "$srcdir"/libarchive-${pkgver}.patch

  arm-none-eabi-cmake -B _3ds -S . -GNinja \
    -DENABLE_TAR=OFF \
    -DENABLE_TEST=OFF \
    -DENABLE_CAT=OFF \
    -DENABLE_CPIO=OFF \
    -DENABLE_UNZIP=OFF

  cmake --build _3ds
}

package() {
  cd libarchive-$pkgver

  source /opt/devkitpro/3dsvars.sh

  DESTDIR="$pkgdir" cmake --install _3ds

  # license
  install -Dm644 "$srcdir"/libarchive-${pkgver}/COPYING "$pkgdir"${PORTLIBS_PREFIX}/licenses/$pkgname/COPYING
  # remove useless stuff
  rm -fr "$pkgdir"${PORTLIBS_PREFIX}/share

}

sha256sums=('7875d49596286055b52439ed42f044bd8ad426aa4cc5aabd96bfe7abb971d5e8'
            'b08920a298b3142e2075379542d85e6471baac634034b25009d6b5c5d30d56e1')
