# Maintainer:  Dave Murphy <davem@devkitpro.org>
# Contributor: cpasjuste <cpasjuste@gmail.com>
# Contributor: carstene1ns <dev f4ke de>
# Contributor: Pablo Curiel <pabloacurielz@gmail.com>

pkgname=switch-freetype
pkgver=2.13.3
pkgrel=1
pkgdesc='FreeType is a freely available software library to render fonts.'
arch=('any')
url='https://www.freetype.org/'
license=(GPL2+ custom:FreeType)
options=(!strip libtool staticlibs)
depends=("switch-bzip2"
         "switch-libpng"
         "switch-zlib"
)
groups=('switch-portlibs')
makedepends=('switch-pkg-config' 'dkp-toolchain-vars')
source=("http://download.savannah.gnu.org/releases/freetype/freetype-$pkgver.tar.xz")

prepare() {
  cd freetype-$pkgver

  source /opt/devkitpro/switchvars.sh

  aarch64-none-elf-cmake -B _build -GNinja
}

build() {
  cd freetype-$pkgver

  source /opt/devkitpro/switchvars.sh

  aarch64-none-elf-cmake --build _build

}

package() {
  cd freetype-$pkgver

  source /opt/devkitpro/switchvars.sh

  DESTDIR="$pkgdir"  aarch64-none-elf-cmake --install _build


  # Licenses
  install -Dm644 "LICENSE.TXT" "${pkgdir}/${PORTLIBS_PREFIX}/licenses/${pkgname}/LICENSE.TXT"
  install -Dm644 "docs/GPLv2.TXT"   "${pkgdir}/${PORTLIBS_PREFIX}/licenses/${pkgname}/GPLv2.TXT"
  install -Dm644 "docs/FTL.TXT"     "${pkgdir}/${PORTLIBS_PREFIX}/licenses/${pkgname}/FTL.TXT"
}

sha256sums=('0550350666d427c74daeb85d5ac7bb353acba5f76956395995311a9c6f063289')
