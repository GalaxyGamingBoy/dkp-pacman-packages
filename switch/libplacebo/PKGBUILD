pkgbasename=libplacebo
pkgname=switch-${pkgbasename}
pkgver=6.338.2
vulkanver=1.3.283
pkgrel=1
pkgdesc="Reusable library for GPU-accelerated video/image rendering primitives"
arch=('any')
url="https://code.videolan.org/videolan/libplacebo"
license=('LGPL2.1')
options=(!strip libtool staticlibs)
source=(
  "${url}/-/archive/v${pkgver}/${pkgbasename}-v${pkgver}.tar.gz"
  "vulkan-headers-${vulkanver}.tar.gz::https://github.com/KhronosGroup/Vulkan-Headers/archive/v${vulkanver}.tar.gz"
)
groups=('switch-portlibs')
makedepends=("dkp-toolchain-vars" "dkp-meson-scripts")

build() {
  cd ${srcdir}/${pkgbasename}-v${pkgver}

  cp -r ${srcdir}/Vulkan-Headers-${vulkanver}/include/* src/include

  /opt/devkitpro/meson-cross.sh switch ../crossfile.txt \
    -Dopengl=disabled -Dgl-proc-addr=disabled \
    -Dd3d11=disabled -Dglslang=disabled -Dshaderc=disabled \
    -Ddovi=disabled -Dlibdovi=disabled \
    -Dlcms=disabled -Dvulkan=disabled \
    -Ddemos=false -Dtests=false build

  meson compile -C build
}

package() {
  cd ${srcdir}/${pkgbasename}-v${pkgver}

  DESTDIR="$pkgdir" meson install -C build
}
sha256sums=('d029adbe55bba8aed7aed2c48b0b66081dddfb9d42683a709342e33aa666c544'
            'a76ff77815012c76abc9811215c2167128a73a697bcc23948e858d1f7dd54a85')
