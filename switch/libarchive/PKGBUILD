# Maintainer:  Dave Murphy <davem@devkitpro.org>

pkgname=switch-libarchive
pkgver=3.7.2
pkgrel=7
pkgdesc='library that can create and read several streaming archive formats'
arch=('any')
url="https://www.libarchive.org"
license=('Simplified BSD')
options=(!strip libtool staticlibs)
source=(
 "http://www.libarchive.org/downloads/libarchive-${pkgver}.tar.gz"
 "libarchive-3.7.2.patch"
)

makedepends=('switch-pkg-config' 'dkp-toolchain-vars')
depends=('switch-bzip2' 'switch-zlib' 'switch-liblzma' 'switch-lz4' 'switch-libexpat' 'switch-libzstd')
groups=('switch-portlibs')

build() {
  cd libarchive-$pkgver

  source /opt/devkitpro/switchvars.sh

  patch -Np1 -i "$srcdir"/libarchive-${pkgver}.patch

  aarch64-none-elf-cmake -B _build -S . -GNinja \
    -DENABLE_CAT=OFF \
    -DENABLE_CPIO=OFF \
    -DENABLE_TAR=OFF \
    -DENABLE_UNZIP=OFF \
    -DENABLE_ZSTD=ON \
    -DENABLE_OPENSSL=OFF \
    -DENABLE_LIBXML2=OFF \
    -DENABLE_ACL=OFF \
    -DENABLE_TEST=OFF \
    -DBUILD_TESTING=OFF

  cmake --build _build
}

package() {
  cd libarchive-$pkgver

  source /opt/devkitpro/switchvars.sh

  DESTDIR="$pkgdir" cmake --install _build

  # license
  install -Dm644 "$srcdir"/libarchive-${pkgver}/COPYING  "$pkgdir"${PORTLIBS_PREFIX}/licenses/$pkgname/LICENSE
  # remove useless stuff
  rm -r "$pkgdir"${PORTLIBS_PREFIX}/share
}

sha256sums=('df404eb7222cf30b4f8f93828677890a2986b66ff8bf39dac32a804e96ddf104'
            '975c46e9ce765407fb369ec369a2e7f746801f801238a830a38ec61676929fa3')
