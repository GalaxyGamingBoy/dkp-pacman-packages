# Maintainer: Rhys Koedijk <rhys@koedijk.co.nz>

pkgbasename=glu
pkgname=switch-$pkgbasename
pkgver=9.0.3
pkgrel=1
pkgdesc='SGI implementation of the OpenGL Utility library'
arch=('any')
url='https://mesa.freedesktop.org/archive/glu/'
license=('custom')
options=(!strip libtool staticlibs)
source=(
  "${url}/${pkgbasename}-${pkgver}.tar.xz"
  "glu-9.0.3-meson-enable-egl-arg.patch"
  "LICENSE"
)
depends=('switch-mesa')
makedepends=("switch-pkg-config" "dkp-toolchain-vars" "dkp-meson-scripts")
groups=('switch-portlibs')

prepare() {
  cd ${pkgbasename}-${pkgver}
  patch -Np2 -i "${srcdir}/glu-9.0.3-meson-enable-egl-arg.patch"
}

build() {
  cd ${pkgbasename}-${pkgver}

  source /opt/devkitpro/switchvars.sh

  /opt/devkitpro/meson-cross.sh switch ../crossfile.txt \
    -Dgl_provider=egl \
    build

}

package() {
  cd ${pkgbasename}-${pkgver}

  DESTDIR="$pkgdir" meson install -C build
  install -dm755 "$pkgdir"/opt/devkitpro/portlibs/switch/licenses/$pkgname
  install -m644 "${srcdir}"/LICENSE "$pkgdir"/opt/devkitpro/portlibs/switch/licenses/$pkgname/

}

sha256sums=('bd43fe12f374b1192eb15fe20e45ff456b9bc26ab57f0eee919f96ca0f8a330f'
            '5dccad835d8229d8a10daa87b70fd2fddd193702df23fd47b1934d6721acc52c'
            '0d27597d42ef0c1ad0e52887de19d3c985608daa60581e9c075c1a978aa5bf40')

