# Maintainer:  Dave Murphy <davem@devkitpro.org>

pkgname=ppc-freetype
pkgver=2.13.3
pkgrel=2
pkgdesc='FreeType is a freely available software library to render fonts.'
arch=('any')
url='https://www.freetype.org/'
license=(GPL2+ custom:FreeType)
options=(!strip libtool staticlibs)
groups=('ppc-portlibs')
depends=("ppc-bzip2"
         "ppc-libpng"
         "ppc-zlib"
)
makedepends=('ppc-pkg-config' 'dkp-toolchain-vars')
source=("http://download.savannah.gnu.org/releases/freetype/freetype-$pkgver.tar.xz")

prepare() {
  cd freetype-$pkgver

  source /opt/devkitpro/ppcvars.sh

  powerpc-eabi-cmake -S . -B _build \
    -D FT_DISABLE_HARFBUZZ=TRUE \
    -DCMAKE_INSTALL_PREFIX=/opt/devkitpro/portlibs/ppc \
    -GNinja
}

build() {
  cd freetype-$pkgver

  source /opt/devkitpro/ppcvars.sh

  powerpc-eabi-cmake --build _build
}

package() {
  cd freetype-$pkgver

  source /opt/devkitpro/ppcvars.sh

  DESTDIR="$pkgdir"  powerpc-eabi-cmake --install _build
  # Licenses
  install -Dm644 "LICENSE.TXT" "${pkgdir}/${PORTLIBS_PREFIX}/licenses/${pkgname}/LICENSE.TXT"
  install -Dm644 "docs/GPLv2.TXT"   "${pkgdir}/${PORTLIBS_PREFIX}/licenses/${pkgname}/GPLv2.TXT"
  install -Dm644 "docs/FTL.TXT"     "${pkgdir}/${PORTLIBS_PREFIX}/licenses/${pkgname}/FTL.TXT"

}

sha256sums=('0550350666d427c74daeb85d5ac7bb353acba5f76956395995311a9c6f063289')
