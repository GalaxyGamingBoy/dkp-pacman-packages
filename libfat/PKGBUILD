# Maintainer: Dave Murphy <davem@devkitpro.org>

pkgbasename=('libfat')

if [ -z "$PLATFORM" ];then
  PLATFORM=nds
fi

if [ "$PLATFORM" = "gba" ]; then
  platdesc="Nintendo GBA"
  depends=('devkitARM' 'libgba')
  groups=('gba-dev')
  _target=gba
elif [ "$PLATFORM" = "nds" ]; then
  platdesc="Nintendo NDS"
  depends=('libnds')
  groups=('nds-dev')
  _target=ds
elif [ "$PLATFORM" = "ogc" ]; then
  platdesc="Nintendo GameCube/Wii"
  depends=('libogc')
  groups=('wii-dev' 'gamecube-dev')
  _target="wii cube"
else
  echo "$PLATFORM unsupported"
  exit 1
fi

pkgname=${pkgbasename}-${PLATFORM}
pkgver=2.1.0
pkgrel=1
pkgdesc="libdvm"
arch=('any')
license=('ZPL')
url="http://github.com/devkitpro/"
options=(!strip libtool staticlibs)
fatfs_mod_ver=0.15.3
libdvm_ver=2.1.0

source=(
  libdvm-${libdvm_ver}.tar.gz::${url}/libdvm/archive/v${libdvm_ver}.tar.gz
  fatfs-mod-${fatfs_mod_ver}.tar.gz::${url}/fatfs-mod/archive/v${fatfs_mod_ver}.tar.gz
)

makedepends=('catnip' 'devkitARM' 'devkitarm-rules' 'devkitarm-crtls' 'libnds')

if [ "$PLATFORM" = "nds" ]; then
  replaces+=('libfilesystem')
  conflicts+=('libfilesystem')
  provides=('libfilesystem')
fi

prepare() {
  mv ${srcdir}/fatfs-mod-${fatfs_mod_ver} ${srcdir}/libdvm-${libdvm_ver}/fatfs
}

package() {

  cd $srcdir/libdvm-${libdvm_ver}
  DESTDIR=$pkgdir catnip install ${_target}

}

sha256sums=('f3fc17671c8234b345a8b742252d4f26bbe481d8f73b6f990b0d820e97a0616f'
            '26f55e648df6b5cc05204d9288b0c93d30572872c834c2a861b591bad1a3997f')
