# Maintainer: Dave Murphy <davem@devkitpro.org>

pkgbasename=('libfat')

if [ -z "$PLATFORM" ];then
  PLATFORM=nds
fi

if [ "$PLATFORM" = "gba" ]; then
  platdesc="Nintendo GBA"
  depends=('devkitARM' 'libgba')
  groups=('gba-dev')
  _target=gba
elif [ "$PLATFORM" = "nds" ]; then
  platdesc="Nintendo NDS"
  depends=('libnds')
  groups=('nds-dev')
  _target=ds
elif [ "$PLATFORM" = "ogc" ]; then
  platdesc="Nintendo GameCube/Wii"
  depends=('libogc')
  groups=('wii-dev' 'gamecube-dev')
  _target="wii cube"
else
  echo "$PLATFORM unsupported"
  exit 1
fi

pkgname=${pkgbasename}-${PLATFORM}
pkgver=2.0.2
pkgrel=1
pkgdesc="libdvm"
arch=('any')
license=('ZPL')
url="http://github.com/devkitpro/"
options=(!strip libtool staticlibs)
fatfs_mod_ver=0.15.2
libdvm_ver=2.0.1

source=(
  libdvm-${libdvm_ver}.tar.gz::${url}/libdvm/archive/v${libdvm_ver}.tar.gz
  fatfs-mod-${fatfs_mod_ver}.tar.gz::${url}/fatfs-mod/archive/v${fatfs_mod_ver}.tar.gz
)

makedepends=('catnip' 'devkitARM' 'devkitarm-rules' 'devkitarm-crtls' 'libnds')

if [ "$PLATFORM" = "nds" ]; then
  replaces+=('libfilesystem')
  conflicts+=('libfilesystem')
  provides=('libfilesystem')
fi

prepare() {
  mv ${srcdir}/fatfs-mod-${fatfs_mod_ver} ${srcdir}/libdvm-${libdvm_ver}/fatfs
}

package() {

  cd $srcdir/libdvm-${libdvm_ver}
  DESTDIR=$pkgdir catnip install ${_target}

}

sha256sums=('fd6a28429c5462050b76d62e19bd77ae301c43298676915d4a6d1e55dc31abe6'
            'cdd4088ea64332e089f7a378f365ab3729bdcb7c174010ed4a68bc142f6647ce')
