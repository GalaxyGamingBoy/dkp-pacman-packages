# Maintainer:  Dave Murphy <davem@devkitpro.org>
# Contributor: Sergio Padrino <sergio.padrino@gmail.com>

pkgname=gamecube-sdl
pkgver=1.2.15
pkgrel=18
pkgdesc="A library for portable low-level access to a video framebuffer, audio output, mouse, and keyboard."
arch=('any')
url="https://libsdl.org"
license=("LGPL")
options=(!strip libtool staticlibs)
makedepends=('gamecube-pkg-config' 'dkp-toolchain-vars')
depends=('gamecube-opengx' 'libogc')
source=("${url}/release/SDL-${pkgver}.tar.gz" "SDL-1.2.15.patch" "config.site")
groups=('gamecube-portlibs' 'gamecube-sdl-libs')

build() {
  cd SDL-$pkgver

  source /opt/devkitpro/cubevars.sh

  patch -p1 -i $srcdir/SDL-1.2.15.patch
  ./autogen.sh

  CONFIG_SITE=${srcdir}/config.site ./configure --prefix="${PORTLIBS_PREFIX}" --host=powerpc-eabi \
    --disable-shared --enable-static --enable-nintendo-gamecube
  make
}

package() {
  cd SDL-$pkgver

  source /opt/devkitpro/cubevars.sh

  make DESTDIR="$pkgdir" install

  # remove useless stuff
  rm -r "$pkgdir"${PORTLIBS_PREFIX}/share
}

sha256sums=('d6d316a793e5e348155f0dd93b979798933fb98aa1edebcc108829d6474aad00'
            'c29c4fd9504af85a9bbe005e5b970e9da1db6949536d7bf81de81b3ed90516dc'
            '7a5c15f9024100ed893689133746ca9e3ac2613fef9ee82339ac0a41c0192b1f')
