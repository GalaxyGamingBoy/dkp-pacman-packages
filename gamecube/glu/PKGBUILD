pkgbasename=glu
pkgname=gamecube-$pkgbasename
pkgver=9.0.3
pkgrel=1
pkgdesc='SGI implementation of the OpenGL Utility library'
arch=('any')
url='https://mesa.freedesktop.org/archive/glu/'
license=('custom')
options=(!strip libtool staticlibs)
source=(
  "${url}/${pkgbasename}-${pkgver}.tar.xz"
  "LICENSE"
)
depends=('gamecube-opengx')
makedepends=("gamecube-pkg-config" "dkp-toolchain-vars" "dkp-meson-scripts")
groups=('gamecube-portlibs')

build() {
  cd ${pkgbasename}-${pkgver}

  source /opt/devkitpro/cubevars.sh

  /opt/devkitpro/meson-cross.sh gamecube ../crossfile.txt \
    build

}

package() {
  cd ${pkgbasename}-${pkgver}

  DESTDIR="$pkgdir" meson install -C build
  install -dm755 "$pkgdir"/opt/devkitpro/portlibs/gamecube/licenses/$pkgname
  install -m644 "${srcdir}"/LICENSE "$pkgdir"/opt/devkitpro/portlibs/gamecube/licenses/$pkgname/

}

sha256sums=('bd43fe12f374b1192eb15fe20e45ff456b9bc26ab57f0eee919f96ca0f8a330f'
            '0d27597d42ef0c1ad0e52887de19d3c985608daa60581e9c075c1a978aa5bf40')
